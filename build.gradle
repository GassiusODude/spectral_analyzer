plugins {
	id 'java'
	id 'application'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.openjfx.javafxplugin' version '0.1.0'
	id "com.gradleup.shadow" version "9.3.1"
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.0.26' // Third-party plugin
}

group = 'net.kcundercover'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}
javafx {
    version = "21.0.10"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

shadowJar {
    manifest {
        attributes(
            "Implementation-Title": "Spectral Analyzer",
            "Implementation-Version": project.version, // Essential for Python verification
            "Build-Timestamp": new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date())
        )
    }

    // handle licenses
    from("${project.rootDir}") {
        include "LICENSE"
        include "APACHE_LICENSE-2.0.txt"
        include "EJML_LICENSE-2.0.txt"
		include "JFREECHART_LICENSE-LGPL.txt"
        into "META-INF"
    }

    // prevent overwriting multiple license and notice files
    append 'META-INF/LICENSE'
    append 'META-INF/NOTICE'

    mergeServiceFiles()
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	// Use local maven repo if available (for JDSP dependency)
	mavenLocal()

	// If no local, then pull from repo (Github CI)
    maven {
        url 'https://maven.pkg.github.com/GassiusODude/jdsp'
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }

    mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'net.rgielen:javafx-weaver-spring-boot-starter:2.0.1'

	// Core charting engine
    implementation 'org.jfree:jfreechart:1.5.4'
    // JavaFX specific viewer and components
    implementation 'org.jfree:org.jfree.chart.fx:2.0.1'

	// JDSP
	implementation 'net.kcundercover:jdsp:1.2'

	// Jackson (JSON support to read SigMF Meta)
	implementation 'com.fasterxml.jackson.core:jackson-databind'

	// Common Math (FFT)
	implementation 'org.apache.commons:commons-math3:3.6.1'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}



application {
    mainClass = 'net.kcundercover.spectral_analyzer.MainApp'
}

// Optional: Configure them to show HTML reports
checkstyle {
    toolVersion = "10.12.5"
    ignoreFailures = true
	showViolations = true
}

spotbugs {
    toolVersion = '4.8.3'
}

tasks.named('test') {
	useJUnitPlatform()
}
